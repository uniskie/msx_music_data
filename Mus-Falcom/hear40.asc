10 '== MUSICA BGM PLAYER ==
20 CLEAR3000,&HA500:DEFINT A-Z:DIM I(1):W=40
30 KEY1,"A=USR1(0)"+CHR$(13):KEY6,"A=USR2(&HA500)"+CHR$(13):GOTO 290
40 '== LOAD DATA ==
50 VV=V:FOR V=VV TO 15:GOSUB 620:FOR I=0 TO 50:NEXTI,V
60 ON ERROR GOTO 190
70 P$=F$:GOSUB 600:BLOAD F$+".BGM"
80 ON ERROR GOTO 0
90 A$=F$:GOSUB 220:LA=VAL("&H"+AD$(0)):A0=VAL("&H"+AD$(1))
100 LOCATE 2,22:PRINT"ADDRESS=&H";AD$(0);">&H";AD$(1);
110 LP=0:IF FF$=SPACE$(8) THEN 140
120 A$=FF$:GOSUB 230:IA=VAL("&H"+AD$(0)):A1=VAL("&H"+AD$(1))
130 IF (A0<IA) OR (A1<LA) THEN 150 ELSE BEEP
140 FF$=SPACE$(8):PP$=FF$:PRINT SPC(40):GOTO 170
150 BLOAD FF$+".BGM":LP=1:PP$=FF$:FF$=SPACE$(8)
160 PRINT" / &H";AD$(0);">&H";AD$(1);
170 LOCATE 0,21:PRINT"NOW PLAY=LOOP¢"+P$+"£/INTRO";:IF PP$=SPACE$(8) THEN PRINT" NOTHING  " ELSE PRINT"¢"+PP$+"£"
180 V=VV:GOSUB 620:GOSUB 650:I=USR9(0):RETURN
190 '= FILE NOT FOUND =
200 RESUME 210
210 ON ERROR GOTO 0:RETURN
220 '= ADDRESS CHECK =
230 OPEN A$+".BGM" AS #1 LEN=7:FIELD #1,7 AS A$:GET #1,1:CLOSE #1
240 FOR J=0 TO 1:AD$(J)=HEX$(ASC(MID$(A$,2+J*2,1))+ASC(MID$(A$,3+J*2,1))*256):NEXT
250 RETURN
260 '=SCC WITHOUT SOUND CARTRIDGE MODE =
270 POKE&HD053,&H3E:POKE&HD054,A:POKE&HD055,&H32:POKE&HD056,&H98:POKE&HD057,&HCE:POKE&HD058,0:RETURN 'A=ADDRESS OF GAME CARTRIDGE WITH SCC
280 '=MAIN =
290 MX=(W\13)-1:SCREEN0,,0:WIDTH W:COLOR15,0,0:KEY OFF
300 DEFUSR=&HCE00:DEFUSR1=&HCE06:DEFUSR2=&HCE03:DEFUSR3=&HCE09:DEFUSR9=&H156
310 COLOR=(15,0,0,0):DEFFNA(I)=VPEEK(X*13+I+(Y+2)*W)
320 PRINT"PLAY=[SPACE]/INTRO:SET=[RET].RESET=[BS]":PRINT
330 BLOAD"BGM.BIN":FILES"*.BGM":PP$=SPACE$(8):FF$=PP$
340 A=1:GOSUB 260
350 X=0:Y=0:FOR I=0 TO 7:F$=F$+CHR$(FNA(I)):NEXT
360 GOSUB 570:GOSUB 60:ON STOP GOSUB 710:STOP ON 
370 FOR I=0 TO 7:COLOR=(15,I,I,I):FOR J=0 TO 99:NEXTJ,I
380 A$=INKEY$:ON-(A$="")GOTO 450:S=ASC(A$)-28:I=USR9(0)
390 X=X+(S=1)-(S=0):Y=Y+(S=2)-(S=3)
400 IF X<0 THEN Y=Y-1:X=MX
410 IF X>MX THEN Y=Y+1:X=0
420 IF Y<0 THEN Y=0
430 IF Y>15 THEN Y=15
440 IF FNA(0)=32 THEN X=X-1:GOTO 400
450 F$="":FOR I=0 TO 7:F$=F$+CHR$(FNA(I)):NEXT
460 LOCATE 2,19:PRINT"SELECT:LOOP¢"+F$+"£/INTRO";:IF FF$=SPACE$(8) THEN PRINT" NOTHING  " ELSE PRINT"¢"+FF$+"£"
470 LOCATE X*13,Y+2:PRINT"=SELECT=
480 IFA$="0"THENGOSUB 660
490 IFA$="1"THENGOSUB 600
500 IFA$="-"ANDV<>15THENV=V+1:GOSUB 620
510 IFA$="+"ANDV<>0THENV=V-1:GOSUB 620
520 IFA$=" "THEN GOSUB 40
530 IFA$=CHR$(13) THEN FF$=F$
540 IFA$=CHR$(8) THEN FF$=SPACE$(8)
550 LOCATE X*13,Y+2:PRINT F$
560 GOTO 380
570 '= FIRST SETTING =
580 I=USR(0)
590 RETURN
600 '= BGM STOP =
610 I=USR1(0):RETURN
620 '= VOLUME CHANGE =
630 I=USR3(VARPTR(V))
640 RETURN
650 '= PLAY BGM =
660 POKE&HA500,0:IF LP=1 THEN MA=IA ELSE MA=LA
670 POKE&HA502,MA AND255:POKE&HA503,VAL("&H"+MID$(HEX$(MA),1,2))
680 A=USR2(&HA500):POKE&HCE9C,(LA AND255):POKE&HCE9D,VAL("&H"+MID$(HEX$(LA),1,2))
690 RETURN
700 '= END =
710 SCREEN0:KEY ON :LOCATE0,1:PRINT"A=USR1(0) 'STOP":LOCATE0,0
